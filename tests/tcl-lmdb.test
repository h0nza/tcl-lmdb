# tcl-lmdb.test --
#
#	Tests for tcl-lmdb
#------------------------------------------------------------------------------

lappend auto_path .
if {[lsearch [namespace children] ::tcltest] == -1} {
    package require tcltest 2
    namespace import -force ::tcltest::*
}
loadTestedCommands
package require lmdb

set testdir [makeDirectory lmdbtest]

#-------------------------------------------------------------------------------

test lmdb-1.1 {create a environment, wrong # args} {*}{
    -body {
    lmdb
    }
    -returnCodes error
    -match glob
    -result {wrong # args*}
}

test lmdb-1.2 {create a environment} {*}{
    -body {
	lmdb env
    }
    -result {env0}
}

test lmdb-1.3 {opening it} {*}{
    -body {
        set db [lmdb open -env env0]
    }
    -returnCodes error
    -match glob
    -result {env was not open}
}

test lmdb-1.4 {Setup mapsize, expected interger} {*}{
    -body {
    env0 set_mapsize "mytest"
    }
    -returnCodes error
    -match glob
    -result {expected integer*}
}

test lmdb-1.5 {Setup mapsize} {*}{
    -body {
    env0 set_mapsize 1073741824
    }
    -result {0}
}

test lmdb-1.6 {set_maxdbs, expected interger} {*}{
    -body {
    env0 set_maxdbs "mytest"
    }
    -returnCodes error
    -match glob
    -result {expected integer*}
}

test lmdb-1.7 {set_maxdbs} {*}{
    -body {
    env0 set_maxdbs 10
    }
    -result {0}
}

test lmdb-1.8 {set_maxreaders, expected interger} {*}{
    -body {
    env0 set_maxreaders "mytest"
    }
    -returnCodes error
    -match glob
    -result {expected integer*}
}

test lmdb-1.9 {set_maxreaders} {*}{
    -body {
    env0 set_maxreaders 127
    }
    -result {0}
}

test lmdb-1.10 {Open a environment, wrong # args} {*}{
    -body {
    env0 open
    }
    -returnCodes error
    -match glob
    -result {wrong # args*}
}

test lmdb-1.11 {Open a environment, wrong # args} {*}{
    -body {
    env0 open -path
    }
    -returnCodes error
    -match glob
    -result {wrong # args*}
}

test lmdb-1.12 {Open a environment, expected integer} {*}{
    -body {
    env0 open -path $testdir -mode "Error"
    }
    -returnCodes error
    -match glob
    -result {expected integer*}
}

test lmdb-1.13 {Open a environment} {*}{
    -body {
    env0 open -path $testdir -mode 0664
    }
    -result {0}
}


#-------------------------------------------------------------------------------


test lmdb-2.1 {Open a database, wrong # args} {*}{
    -body {
    lmdb open -env
    }
    -returnCodes error
    -match glob
    -result {wrong # args*}
}

test lmdb-2.2 {Open a database} {*}{
    -body {
    lmdb open -env env0
    }
    -result {dbi0}
}

test lmdb-2.3 {create a transaction, wrong # args} {*}{
    -body {
    env0 txn -readonly
    }
    -returnCodes error
    -match glob
    -result {wrong # args*}
}

test lmdb-2.4 {create a transaction} {*}{
    -body {
    env0 txn
    }
    -result {env0.txn0}
}

test lmdb-2.5 {Put, wrong # args} {*}{
    -body {
    dbi0 put "1234" "567890" -txn
    }
    -returnCodes error
    -match glob
    -result {wrong # args*}
}

test lmdb-2.6 {Put} {*}{
    -body {
    dbi0 put "1234" "567890" -txn env0.txn0
    }
    -result {0}
}

test lmdb-2.7 {Get, wrong # args} {*}{
    -body {
    dbi0 get "1234" -txn
    }
    -returnCodes error
    -match glob
    -result {wrong # args*}
}

test lmdb-2.8 {Get} {*}{
    -body {
    dbi0 get "1234" -txn env0.txn0
    }
    -result {567890}
}

test lmdb-2.9 {Cursor, wrong # args} {*}{
    -body {
    dbi0 cursor -txn
    }
    -returnCodes error
    -match glob
    -result {wrong # args*}
}

test lmdb-2.10 {Cursor} {*}{
    -body {
    dbi0 cursor -txn env0.txn0
    }
    -result {dbi0.c0}
}

test lmdb-2.11 {Cursor put, wrong # args} {*}{
    -body {
    dbi0.c0 put "5678" "123490" -append
    }
    -returnCodes error
    -match glob
    -result {wrong # args*}
}

test lmdb-2.12 {Cursor put} {*}{
    -body {
    dbi0.c0 put "5678" "123490" -append 1
    }
    -result {0}
}

test lmdb-2.13 {Cursor del, wrong # args} {*}{
    -body {
    dbi0.c0 del -nodupdata
    }
    -returnCodes error
    -match glob
    -result {wrong # args*}
}

test lmdb-2.14 {Cursor del} {*}{
    -body {
    dbi0.c0 get -last
    dbi0.c0 del
    }
    -result {0}
}

test lmdb-2.15 {Cursor put, wrong # args} {*}{
    -body {
    dbi0.c0 put "這是鍵"
    }
    -returnCodes error
    -match glob
    -result {wrong # args*}
}

test lmdb-2.16 {Cursor put, UTF8} {*}{
    -body {
    dbi0.c0 put "這是鍵" "這是值" -append 1
    }
    -result {0}
}

test lmdb-2.17 {Cursor get, UTF8} {*}{
    -body {
	lindex [dbi0.c0 get -set "這是鍵"] 1
    }
    -result {這是值}
}

test lmdb-2.18 {Cursor get, not found} {*}{
    -body {
        dbi0.c0 get -set "13579"
    }
    -returnCodes error
    -match glob
    -result {*No matching key/data pair found}
}

test lmdb-2.19 {Cursor close} {*}{
    -body {
    dbi0.c0 close
    }
    -result {0}
}

test lmdb-2.20 {Delete, wrong # args} {*}{
    -body {
    dbi0 del "1234" "567890" -txn
    }
    -returnCodes error
    -match glob
    -result {wrong # args*}
}

test lmdb-2.21 {Delete, wrong # args} {*}{
    -body {
    dbi0 del "1234" "567890" -test env0.txn0 -txn
    }
    -returnCodes error
    -match glob
    -result {wrong # args*}
}

test lmdb-2.22 {Delete} {*}{
    -body {
    dbi0 del "1234" "" -txn env0.txn0
    }
    -result {0}
}

test lmdb-2.23 {Drop, wrong # args} {*}{
    -body {
    dbi0 drop "mytest" -txn env0.txn0
    }
    -returnCodes error
    -match glob
    -result {expected integer*}
}

test lmdb-2.24 {Drop} {*}{
    -body {
    dbi0 drop 0 -txn env0.txn0
    }
    -result {0}
}

test lmdb-2.25 {Commit} {*}{
    -body {
    env0.txn0 commit
    }
    -result {0}
}

test lmdb-2.26 {UTF8} {*}{
    -body {
    set k {Linn‘us}
    set v {ℕ ⊆ ℕ₀ ⊂ ℤ ⊂ ℚ ⊂ ℝ ⊂ ℂ}
    set mytxn [env0 txn]
    dbi0 put $k $v -txn $mytxn
    $mytxn commit
    $mytxn close
    set mytxn [env0 txn -readonly 1]
    set r [dbi0 get $k -txn $mytxn]
    $mytxn abort
    $mytxn close
    string compare $r $v
    } -result 0
}

test lmdb-2.27 {Binary key and value} {*}{
    -body {
    # generate 255 random bytes
    for {set i 1} {$i < 256} {incr i} {
        set ch [expr {int(rand()*100)}]
        append txt [binary format c $ch]
    }
    set k $txt
    set v $k
    set mytxn [env0 txn]
    dbi0 put $k $v -txn $mytxn
    $mytxn commit
    $mytxn close
    set mytxn [env0 txn -readonly 1]
    set r [dbi0 get $k -txn $mytxn]
    $mytxn abort
    $mytxn close
    expr {$r eq $k && $r eq $v}
    } -result 1
}

#-------------------------------------------------------------------------------

test lmdb-3.1 {PutBinary, wrong # args} {*}{
    -body {
        set mytxn2 [env0 txn]
        dbi0 putBinary [encoding convertto utf-8 "這是鍵"]
    }
    -returnCodes error
    -match glob
    -result {wrong # args*}
}


test lmdb-3.2 {PutBinary} {*}{
    -body {
    dbi0 putBinary [encoding convertto utf-8 "這是鍵"] \
	       [encoding convertto utf-8 "這是值"] -txn $mytxn2
    }
    -result {0}
}

test lmdb-3.3 {GetBinary} {*}{
    -body {
        set data [dbi0 getBinary [encoding convertto utf-8 "這是鍵"] -txn $mytxn2]
        encoding convertfrom utf-8 $data
    }
    -result {這是值}
}

test lmdb-3.4 {DeleteBinary} {*}{
    -body {
    dbi0 delBinary [encoding convertto utf-8 "這是鍵"] \
	       [encoding convertto utf-8 "這是值"] -txn $mytxn2
    }
    -result {0}
}

test lmdb-3.5 {Cursor} {*}{
    -body {
    dbi0 cursor -txn $mytxn2
    }
    -result {dbi0.c1}
}


test lmdb-3.6 {Cursor putBinary} {*}{
    -body {
    dbi0.c1 putBinary [encoding convertto utf-8 "這是鍵"] \
	          [encoding convertto utf-8 "這是值"] -append 1
    }
    -result {0}
}

test lmdb-3.7 {Cursor getBinary} {*}{
    -body {
	set data [lindex [dbi0.c1 getBinary -last] 1]
	encoding convertfrom utf-8 $data
    }
    -result {這是值}
}

test lmdb-3.8 {Cursor close} {*}{
    -body {
    dbi0.c1 close
    $mytxn2 commit
    $mytxn2 close
    }
    -result {0}
}

#-------------------------------------------------------------------------------

catch {env0.txn0 close}
catch {dbi0 close}
catch {env0 close}

removeFile data.mdb lmdbtest
removeFile lock.mdb lmdbtest
removeDirectory lmdbtest

cleanupTests
return
